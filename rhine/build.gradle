apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion build_version.compileSdkVersion

    defaultConfig {
        minSdkVersion build_version.minSdkVersion
        targetSdkVersion build_version.targetSdkVersion
        versionCode build_version.versionCode
        versionName build_version.versionName

        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // support
    implementation libs.support_v7
    implementation libs.design
    implementation libs.recyclerview

    implementation libs.kotlin_stdlib

    // jetpack
    api libs.arch_comp_extension

    api libs.arch_comp_room
    api libs.arch_comp_room_rxjava
    kapt libs.arch_comp_room_compiler

    api libs.arch_comp_navigation
    api libs.arch_comp_navigation_ui
    kapt libs.arch_comp_compiler

    // retrofit
    api libs.retrofit
    api libs.retrofit_gson
    api libs.retrofit_rxjava

    // rx
    api libs.rxkotlin
    api libs.rxandroid
    api libs.rxbinding
    api libs.rxbinding_v7
    api libs.rxpermissions
    api libs.rxweaver

    // autodispose
    api libs.autodispose
    api libs.autodispose_ktx
    api libs.autodispose_archs_ktx

    // kodein
    api libs.kodein
    api libs.kodein_android
    api libs.kodein_android_support

    // arrow
    api libs.arrow

    // okhttp
    api libs.okhttp
    api libs.okhttp_logging

    // glide
    api libs.glide
    kapt libs.glide_compiler

    kapt "com.android.databinding:compiler:${versions.android_plugin}"

    // logger
    api libs.timber

    // recyclerview_binding
    api libs.dsl_adapter_binding, {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

    testImplementation libs.junit
    androidTestImplementation libs.espresso_core
}

kapt {
    generateStubs = true
}

def publishMode = false

version = build_version.versionName
group = "com.github.qingmei2.rhine"
def siteUrl = "https://github.com/qingmei2/MVVM-Rhine"
def gitUrl = "https://github.com/qingmei2/MVVM-Rhine.git"
def libName = "mvvm-rhine"
def projectName = "MVVM-Rhine"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name projectName
                url siteUrl
                licenses {
                    license {
                        name 'Apache license'
                        url 'https://github.com/qingmei2/MVVM-Rhine#License'
                    }
                }
                developers {
                    developer {
                        id 'qingmei2'
                        name 'qingmei2'
                        email 'mq2553299@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    failOnError false
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

if (publishMode) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

bintray {
    user = publishMode ? properties.getProperty("bintray.user") : ""
    key = publishMode ? properties.getProperty("bintray.apikey") : ""
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = libName
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}